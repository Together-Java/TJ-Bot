plugins {
    id "com.google.cloud.tools.jib" version "3.1.4"
    id "org.springframework.boot" version "2.6.1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.vaadin" version "23.0.3"
    id 'java'
    id 'database-settings'
}

defaultTasks("clean", "bootRun")

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.vaadin.com/vaadin-addons")
    }
}

configurations {
    developmentOnly
    runtimeClasspath.extendsFrom(developmentOnly)
}

jooq {
    configurations {
        main {
            generationTool {
                generator {
                    target {
                        packageName = 'org.togetherjava.tjbot.db.generated'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation('org.apache.logging.log4j:log4j-api') {
        version {
            require '2.16.0'
        }
        because 'Log4Shell happened'
    }
    runtimeOnly('org.apache.logging.log4j:log4j-core') {
        version {
            require '2.16.0'
        }
        because 'Log4Shell happened'
    }

    runtimeOnly 'org.apache.logging.log4j:log4j-jul:2.16.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.16.0'


    implementation(project(":database"))
    implementation 'org.jooq:jooq:3.15.3'

    implementation 'com.vaadin:vaadin-core:21.0.2'
    implementation('com.vaadin:vaadin-spring:18.0.0')
    implementation 'org.vaadin.artur:a-vaadin-helper:1.7.2'
    implementation 'org.vaadin.crudui:crudui:4.6.0'
    implementation 'com.vaadin.componentfactory:enhanced-dialog:21.0.0'


    implementation('org.springframework.boot:spring-boot-starter-web:2.6.1') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation('org.springframework.boot:spring-boot-starter-security:2.6.1') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation('org.springframework.boot:spring-boot-starter-oauth2-client:2.6.1') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    developmentOnly('org.springframework.boot:spring-boot-starter-actuator:2.6.1') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    developmentOnly('org.springframework.boot:spring-boot-devtools:2.6.1') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

tasks.jib.dependsOn(tasks.vaadinBuildFrontend)
jib {
    from.image = 'eclipse-temurin:17'
    to {
        image = 'togetherjava.duckdns.org:5001/togetherjava/tjlogs:' + System.getenv('BRANCH_NAME') ?: 'latest'
        auth {
            username = System.getenv('REGISTRY_USER') ?: ''
            password = System.getenv('REGISTRY_PASSWORD') ?: ''
        }
    }
    container {
        setPorts(["5050"].asList())
        setCreationTime(Instant.now().toString())
    }
}

vaadin {
    pnpmEnable = true
    productionMode = true
}
