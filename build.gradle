plugins {
    id 'java'
    id "com.diffplug.spotless" version "5.15.0"
    id "org.sonarqube" version "3.3"
}

group 'org.togetherjava'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

project.tasks["sonarqube"].dependsOn "build"

sonarqube {
    properties {
        property "sonar.projectKey", "Together-Java_TJ-Bot"
        property "sonar.organization", "togetherjava"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.sonarqube"

    compileJava.dependsOn 'spotlessApply'

    spotless {
        java {
            targetExclude("build/**")
            eclipse().configFile("${rootProject.rootDir}/meta/formatting/google-style-eclipse.xml")
        }
    }

    configurations {
        umlDoclet
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        umlDoclet 'nl.talsmasoftware:umldoclet:2.0.14'
    }

    javadoc {
        exclude 'build/generated/**'
        exclude 'build/generated-src/**'
        source = sourceSets.main.allJava
//        destinationDir = reporting.file("docs/uml")
        options.docletpath = configurations.umlDoclet.files as List
        options.doclet = "nl.talsmasoftware.umldoclet.UMLDoclet"
//    options.addStringOption "private"
        options.addStringOption "-show-members", "private"
    }
}
