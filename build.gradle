plugins {
    id 'java'
    id "com.diffplug.spotless" version "5.15.0"
    id "org.sonarqube" version "3.3"
    id "name.remal.sonarlint" version "1.5.0"
}

group 'org.togetherjava'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

// Skips sonarlint during the build, useful for testing purposes.
Boolean skipSonarlint = false;

// Sonarqube task should be ran last, so it can collect all the useful data during the build.
project.tasks["sonarqube"].dependsOn "build"
sonarqube {
    properties {
        property "sonar.projectKey", "Together-Java_TJ-Bot"
        property "sonar.organization", "togetherjava"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.sonarqube"

    if(!skipSonarlint) {
        apply plugin: "name.remal.sonarlint"
    }

    // All subprojects inherit root project group and version, to avoid duplication.
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    // Makes spotlessApply task run on every compile/build.
    compileJava.dependsOn 'spotlessApply'
    spotless {
        java {
            // Excludes build folder since it contains generated java classes.
            targetExclude("build/**")
            eclipse().configFile("${rootProject.rootDir}/meta/formatting/google-style-eclipse.xml")
        }
    }
}
