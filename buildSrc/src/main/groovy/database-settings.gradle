plugins {
    id "org.flywaydb.flyway"
    id 'nu.studer.jooq'
}

//This cannot be rootDir.buildDir since then different Modules would share 1 DB
//And Only 1 of them would work and every other would fail because it contains unknown Tables/Entities
var databaseFile = new File(buildDir, "database.db")
var databaseUrl = "jdbc:sqlite:${databaseFile}"

tasks.register("createBuildDirectoryIfNeeded") {
    doLast {
        if (!databaseFile.parentFile.exists()) {
            databaseFile.parentFile.mkdirs()
        }
    }
    // https://github.com/gradle/gradle/issues/2488
    mustRunAfter(project.tasks.findByPath('clean'))
}

flyway {
    url = databaseUrl
    locations = ["filesystem:src/main/resources/db"]
}

tasks.flywayMigrate {
    dependsOn("createBuildDirectoryIfNeeded")
}

jooq {
    version = jooqVersion

    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = databaseUrl
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                        excludes = 'flyway_schema_history|sqlite_sequence'

                        forcedTypes {
                            forcedType {
                                name = 'INSTANT'
                                includeTypes = 'TIMESTAMP'
                            }
                        }
                    }
                    generate {
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'org.togetherjava.tjbot.db.generated'
                    }
                }
            }
        }
    }
}

tasks.generateJooq {
    dependsOn("flywayMigrate")
}

var sqliteVersion = "3.36.0.3"

dependencies {
    implementation "org.xerial:sqlite-jdbc:${sqliteVersion}"
    implementation 'org.flywaydb:flyway-core:8.0.0'
    implementation "org.jooq:jooq:$jooqVersion"

    jooqGenerator "org.xerial:sqlite-jdbc:${sqliteVersion}"
}
